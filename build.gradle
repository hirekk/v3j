plugins {
    id 'java'
    id 'application'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'jacoco'
}

java {
    sourceCompatibility = JavaVersion.VERSION_24
    targetCompatibility = JavaVersion.VERSION_24
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

// Modern source set configuration
sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

repositories {
    mavenCentral()
}

dependencies {
  implementation 'info.picocli:picocli:4.7.7'
  implementation 'org.apache.commons:commons-math3:3.6.1'
  testImplementation platform('org.junit:junit-bom:5.13.4')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
    mainClass = 'data.Main'
}

test {
    useJUnitPlatform()
    
    // Show test results with summary
    testLogging {
        events = ["failed"]
        exceptionFormat = "full"
        showStackTraces = true
        showStandardStreams = false
    }
}

// Modern task configuration
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked']
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

test.finalizedBy jacocoTestReport
